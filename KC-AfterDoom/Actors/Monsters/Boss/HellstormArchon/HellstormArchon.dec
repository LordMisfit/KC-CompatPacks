ACTOR ADHellstormArchon  30104
{
//$Category "After Doom Monster"
Health 8000 
Radius 40 
Height 110
scale 2 
Mass 1000 
Speed 16 
PainChance 20 
MONSTER
MeleeDamage 10
Obituary "%o was no match for an archon of hell."
HitObituary "%o was ripped apart by an archon of hell."
BloodColor "20 60 20"
tag "Hell Storm"
+FLOORCLIP 
+MISSILEMORE
+NORADIUSDMG
+BOSS
+DONTHURTSPECIES
SeeSound "monster/arcsit" 
PainSound "baron/pain" 
DeathSound "monster/arcdth"
MeleeSound "baron/melee"
DamageFactor "Bum", 0.0
Species "Icon"
States 
   { 
   Spawn: 
       GOS3 AB 10 A_Look 
       Loop 
   See:
       GOS3 A 3
	   TNT1 A 0 A_Quake(2, 24, 0, 800, "apocalipsis/APOKPISA")
       GOS3 ABBCC 3 A_Chase 
	   GOS3 D 3
	   TNT1 A 0 A_Quake(2, 24, 0, 800, "apocalipsis/APOKPISA")
	   GOS3 D 3 A_Chase	   
       Loop 
   Missile:
       GOS3 B 0 A_Jump(128,13)
       GOS3 EF 8 A_FaceTarget 
       GOS3 G 0 A_CustomMissile ("BaronBall",68,0,0,0)
       GOS3 G 0 A_CustomMissile ("BaronBall",68,0,-4,0)
       GOS3 G 6 A_CustomMissile ("BaronBall",68,0,4,0)
       GOS3 E 0 A_Jump(64,7)
       GOS3 ST 8 A_FaceTarget 
       GOS3 U 0 A_CustomMissile ("BaronBall",68,0,0,0)
       GOS3 U 0 A_CustomMissile ("BaronBall",68,0,-4,0)
       GOS3 U 6 A_CustomMissile ("BaronBall",68,0,4,0)
       GOS3 S 0 A_Jump(64,1)
       Goto See
       GOS3 HI 8 A_FaceTarget
       GOS3 S 0 A_Jump(128,2)
       GOS3 J 8 A_CustomMissile ("ArchonComet",68,0,0,0)
       Goto See
       GOS3 J 0 A_CustomMissile ("BaronBall",68,0,-6,0)
       GOS3 J 0 A_CustomMissile ("BaronBall",68,0,-4,0)
       GOS3 J 0 A_CustomMissile ("BaronBall",68,0,-2,0)
       GOS3 J 0 A_CustomMissile ("BaronBall",68,0,2,0)
       GOS3 J 0 A_CustomMissile ("BaronBall",68,0,4,0)
       GOS3 J 6 A_CustomMissile ("BaronBall",68,0,6,0)
       Goto See
   Melee:
       GOS3 EF 8 A_FaceTarget 
       GOS3 G 6 A_MeleeAttack
       GOS3 G 0 A_Jump (128,1)
       Goto See
       GOS3 ST 8 A_FaceTarget 
       GOS3 U 6 A_MeleeAttack
       GOS3 G 0 A_Jump (64,1)
       Goto See
       GOS3 HI 8 A_FaceTarget
       GOS3 J 0 A_MeleeAttack
       GOS3 J 6 A_MeleeAttack
       Goto See
   Pain: 
       GOS3 K 2 
       GOS3 K 2 A_Pain 
       Goto See 
   Death: 
       GOS3 L 8 
       GOS3 M 8 A_Scream 
       GOS3 N 8 
       GOS3 O 8 A_NoBlocking 
       GOS3 PQ 8 
       GOS3 R -1 A_Fall 
       Stop
   XDeath:
       GARC A 5
       GARC B 5 A_XScream
       GARC C 5 A_NoBlocking
       GARC DEFGH 5
       GARC I -1
       stop
   Raise: 
       GOS3 RQPONML 8 
       Goto See 
   } 
}

ACTOR ArchonComet
{
   Radius 8
   Height 12
   Speed 25
   Damage 15
   Scale 1.0
   PROJECTILE
   RENDERSTYLE Normal
   SeeSound "weapons/firbfi"
   DeathSound "weapons/hellex"
   States
   {
   Spawn: 
      ARCB AAAABBBBCCCC 1 Bright A_SpawnItem("ArchonCometTrail",0,0)
      loop
   Death:
      ARCB J 0 Bright A_SetTranslucent (0.67,1)
      ARCB J 3 Bright
      ARCB K 3 Bright
      ARCB LMN 3 Bright
      stop
   }
}

ACTOR ArchonCometTrail
{   
   Radius 3
   Height 3
   Scale 0.75
   Speed 0
   PROJECTILE
   RENDERSTYLE ADD
   ALPHA 0.67
   States
   {
   Spawn:
      NULL A 3 Bright
      ARCB DEFGHI 3 BRIGHT
      Stop 
   }
}

ACTOR staticHellstormArchon  10885
{

Health 8000 
Radius 40 
Height 110
scale 2 
Mass 1000 
PainChance 20 
MeleeDamage 10
tag "Hell Storm"
+FLOORCLIP 
+MISSILEMORE
+NORADIUSDMG
+BOSS
+DONTHURTSPECIES
DamageFactor "Bum", 0.0
Species "Icon"
States 
   { 
   Spawn: 
       GOS3 A -1  
       Loop 
   }
}	   